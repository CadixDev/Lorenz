plugins {
    id 'java'
    id 'maven'

    id 'idea'
    id 'eclipse'

    id 'com.github.hierynomus.license' version '0.11.0'
}

defaultTasks 'clean', 'licenseFormat'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'xyz.lexteam'
version = '1.0.0-SNAPSHOT'
description = "Lorenz - Represent Java deobfuscation mappings."
ext.url = 'http://www.lexteam.xyz/'

repositories {
    mavenCentral()
}

dependencies {
}

license {
    header file('HEADER.txt')
    include '**/*.java'
    include '**/*.groovy'

    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'http://lex1.lexteam.xyz:8888/repository/'
                    + (version.endsWith('-SNAPSHOT') ? 'snapshots' : 'internal') + '/') {
                authentication(userName: project.lexRepoUser, password: project.lexRepoPass)
            }

            pom {
                artifactId = project.archivesBaseName.toLowerCase()

                project {
                    name = project.archivesBaseName
                    description = project.description
                    packaging = 'jar'
                    url = project.url

                    scm {
                        url = "https://github.com/Lexteam/Lorenz"
                        connection = "scm:git:https://github.com/Lexteam/Lorenz.git"
                        developerConnection = "scm:git:git@github.com:Lexteam/Lorenz.git"
                    }

                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/Lexteam/Lorenz/issues'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
